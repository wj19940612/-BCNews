apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.sbai.bcnews"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField('String', 'HOST', '\"news.esongbai.abc\"')

        manifestPlaceholders = [
                GETUI_APP_ID    : "yz2rMIFVG88ywlddHHoLU4",
                GETUI_APP_SECRET: "Qr9bNhb5Cn8llNuhj1OZk1",
                GETUI_APP_KEY   : "ZfYQR3S4rSAFq58WDy3ZX2",
                UMENG_APP_KEY   : "59f7e22d734be4113a0000d8",
                UMENG_CHANNEL   : "android"
        ]
    }

    java.util.Properties properties = new Properties();
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    signingConfigs {
        release {
            storeFile file(properties.get('keystore_storeFile'))
            storePassword properties.get('keystore_storePassword')
            keyAlias properties.get('keystore_keyAlias')
            keyPassword properties.get('keystore_keyPassword')
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            ext.enableCrashlytics = false
        }
    }

    aaptOptions {
        cruncherEnabled true // !!set true when package release apk
    }

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
        javaMaxHeapSize '2g'
    }

    flavorDimensions("app")

    productFlavors {
        dev {
            buildConfigField('String', 'HOST', '\"news.esongbai.abc\"')
        }

        alpha {
            versionCode 6
            versionName "1.0.6"
            buildConfigField('String', 'HOST', '\"testnews.esongbai.xyz\"')
        }

        pro {

        }

    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "bcnews-${variant.name}-${variant.versionName}.apk"
        }
    }

    productFlavors.all { flavor ->
        if (flavor.name != 'dev' && flavor.name != 'alpha') {
            flavor.manifestPlaceholders = [UMENG_CHANNEL: name]
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.github.chrisbanes:PhotoView:1.3.1'
    // test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    // third part
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.getui:sdk:2.10.2.0'
    implementation 'com.umeng.analytics:analytics:latest.integration'
    implementation project(path: ':swipeloadlayout')
    implementation project(path: ':httplib')
    implementation project(path: ':glide')
    implementation files('libs/SocialSDK_QQ_Simplify.jar')
}
